plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'hu.supercluster.paperwork'
}

android {
    signingConfigs {
        release {
            storeFile file(System.properties.getOrDefault('RELEASE_STORE_FILE', '../debug.keystore'))
            storePassword System.properties.getOrDefault('RELEASE_STORE_PASSWORD', 'password')
            keyAlias System.properties.getOrDefault('RELEASE_KEY_ALIAS', 'alias_name')
            keyPassword System.properties.getOrDefault('RELEASE_KEY_PASSWORD', 'password')
        }
    }
    namespace 'com.jwoglom.controlx2'
    compileSdk 33

    defaultConfig {
        applicationId "com.jwoglom.controlx2"
        minSdk 30
        targetSdk 33
        versionCode 1
        versionName "${project.controlx2_version}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "${compose_version}"
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

paperwork {
    set = [
        build_time: buildTime("yyyy-MM-dd HH:mm:ss", "UTC"),
        build_version: gitInfo()
    ]
}

dependencies {
    implementation 'hu.supercluster:paperwork:1.2.7'

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.google.android.gms:play-services-wearable:18.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation project(path: ':shared')

    // pumpx2-android dependencies
    implementation 'com.github.weliem:blessed-android:2.4.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation "me.champeau.openbeans:openbeans:1.0.2"
    implementation "commons-codec:commons-codec:1.15"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "com.google.guava:guava:31.0.1-android"

    // pumpx2
    if (project.use_local_pumpx2) {
        implementation files("${project.m2_repository}/com/jwoglom/pumpx2/pumpx2-android/${project.pumpx2_version}/pumpx2-android-${project.pumpx2_version}.aar")
        implementation files("${project.m2_repository}/com/jwoglom/pumpx2/pumpx2-messages/${project.pumpx2_version}/pumpx2-messages-${project.pumpx2_version}.jar")
        implementation files("${project.m2_repository}/com/jwoglom/pumpx2/pumpx2-shared/${project.pumpx2_version}/pumpx2-shared-${project.pumpx2_version}.jar")
    } else {
        implementation "com.github.jwoglom.pumpX2:pumpx2-android:v${project.pumpx2_version}"
        implementation "com.github.jwoglom.pumpX2:pumpx2-messages:v${project.pumpx2_version}"
        implementation "com.github.jwoglom.pumpX2:pumpx2-shared:v${project.pumpx2_version}"
    }

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.compose.material3:material3:1.1.0-alpha03'
    implementation 'androidx.navigation:navigation-compose:2.5.3'
    implementation 'androidx.navigation:navigation-runtime-ktx:2.5.3'
    implementation 'androidx.compose.material:material:1.3.1'
    implementation 'androidx.compose.runtime:runtime-livedata:1.3.1'


    // https://issuetracker.google.com/issues/227767363
    debugImplementation "androidx.customview:customview:1.2.0-alpha02"
    debugImplementation "androidx.customview:customview-poolingcontainer:1.0.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
    wearApp project(":wear")
}